namespace base58

    function encode(inputString as string)
        bytes = createObject("roByteArray")
        bytes.fromHexString(inputString)
        return encodeBytes(bytes)
    end function

    function encodeAscii(inputString as string)
        bytes = createObject("roByteArray")
        bytes.fromAsciiString(inputString)
        return encodeBytes(bytes)
    end function

    function encodeBytes(inputBytes as roByteArray) as string
        ALPHABET = CreateObject("roByteArray")
        ALPHABET.fromAsciiString("123456789ABCDEFGHJKLMNPQRSTUVWXYZabcdefghijkmnopqrstuvwxyz")
        ENCODED_ZERO = ALPHABET[0]
        if inputBytes.Count() = 0 then
            return ""
        end if

        ' Count leading zeros
        zeros = 0
        for i = 0 to inputBytes.Count() - 1
            if inputBytes[i] = 0 then
                zeros = zeros + 1
            else
                exit for
            end if
        next

        ' Convert base-256 digits to base-58 digits
        encoded = createObject("roByteArray")' Upper bound
        encoded.setresize(inputBytes.count() * 2, false)
        outputStart = inputBytes.count() * 2
        inputStart = zeros
        while inputStart < inputBytes.Count()
            outputStart -= 1
            remainder = base58.internal.divmod(inputBytes, inputStart, 256, 58)
            char = ALPHABET[remainder]
            encoded[outputStart] = char
            if inputBytes[inputStart] = 0 then
                inputStart = inputStart + 1 ' Optimization - skip leading zeros
            end if
        end while

        ' Preserve exactly as many leading encoded zeros in output as there were leading zeros in input
        while outputStart < encoded.Count() and encoded[outputStart] = ENCODED_ZERO
            outputStart = outputStart + 1
        end while

        for i = 1 to zeros
            outputStart -= 1
            encoded[outputStart] = ENCODED_ZERO
        next

        result = createObject("roByteArray")
        delta = encoded.count() - outputStart
        for i = 0 to delta
            result[i] = encoded[i + outputStart]
        next
        ' Return encoded string (including encoded leading zeros)
        'return Mid(encoded, outputStart + 1, encoded.Count() - outputStart)
        return result.toAsciiString()
    end function

    ' Just let GPT convert the kotlin code. I don't know what it does
    function decode(inputString as String) as Object
        if Len(inputString) = 0
            return []
        end if
    
        ' Base58 alphabet
        alphabet = "123456789ABCDEFGHJKLMNPQRSTUVWXYZabcdefghijkmnopqrstuvwxyz"
    
        ' Build index map: ASCII -> base58 value
        indexes = CreateObject("roArray", 128, true)
        for i = 0 to 127
            indexes[i] = -1
        end for
        for i = 0 to Len(alphabet) - 1
            c = Mid(alphabet, i + 1, 1)
            indexes[Asc(c)] = i
        end for
    
        ' Convert ASCII characters to base58 digit values
        input58 = []
        for i = 0 to Len(inputString) - 1
            c = Mid(inputString, i + 1, 1)
            asciiVal = Asc(c)
            if asciiVal >= 128 or indexes[asciiVal] < 0
                print "Invalid character in Base58: "; c
                return invalid
            end if
            input58.Push(indexes[asciiVal])
        end for
    
        ' Count leading zeros
        zeros = 0
        while zeros < input58.Count() and input58[zeros] = 0
            zeros += 1
        end while
    
        ' Decode Base58 -> Base256
        decoded = CreateObject("roArray", Len(inputString), true)
        for i = 0 to Len(inputString) - 1
            decoded[i] = 0
        end for
        outputStart = decoded.Count()
    
        inputStart = zeros
        while inputStart < input58.Count()
            digit = base58.internal.DivMod(input58, inputStart, 58, 256)
            outputStart -= 1
            decoded[outputStart] = digit
            if input58[inputStart] = 0
                inputStart += 1
            end if
        end while
    
        ' Skip extra leading zeroes
        while outputStart < decoded.Count() and decoded[outputStart] = 0
            outputStart += 1
        end while
    
        ' Return decoded portion including original leading zeros
        result = []
        for i = 0 to zeros - 1
            result.Push(0)
        end for
        for i = outputStart to decoded.Count() - 1
            result.Push(decoded[i])
        end for
    
        return result
    end function

    namespace internal
        function divmod(number as roByteArray, firstDigit as integer, base as integer, divisor as integer) as integer
            ' This function performs long division, accounting for the base of the input digits
            remainder = 0

            for i = firstDigit to number.Count() - 1
                ' Convert byte to integer and mask to ensure it's treated as unsigned
                digit = (number[i] and &HFF)

                ' Perform division
                temp = remainder * base + digit
                number[i] = int(temp \ divisor)
                remainder = temp mod divisor
            next

            return remainder
        end function
    end namespace

end namespace
