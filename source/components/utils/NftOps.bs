import "pkg:/components/utils/RouterUtils.bs"
import "pkg:/components/utils/Toast.bs"
import "pkg:/components/http/http.bs"

namespace NftOps

    enum MediaType
        ' Fabric gives capitalized media types. A lot of checks would break if this assumption changes.
        AUDIO = "Audio"
        EBOOK = "Ebook"
        GALLERY = "Gallery"
        HTML = "HTML"
        IMAGE = "Image"
        LIVE = "Live Video"
        VIDEO = "Video"
    end enum

    ' Turns an asset container object to a ready-to-use url
    function assetLinkToUrl(assetLinkJson as dynamic, baseUrl = http.BASE_URL) as string
        if assetLinkJson = invalid
            return ""
        end if
        if (assetLinkJson["/"].startsWith("/qfab/"))
            ' When the link contains "qfab", it also has a different "container" value,
            ' in that case we ignore [assetLinkJson["."].container]
            ' remove "/qfab/" prefix (6 chars)
            return `${baseUrl}q/${assetLinkJson["/"].mid(6)}`.EncodeUri()
        else
            hash = assetLinkJson["."].container
            ' remove "./" prefix (2 chars)
            filePath = assetLinkJson["/"].mid(2)
            return `${baseUrl}q/${hash}/${filePath}`.EncodeUri()
        end if
    end function

    function nftsToContentNode(nfts, rootNode = createObject("roSGNode", "ContentNode"), contentType = "nft") as roSGNode
        for each nft in nfts
            node = rootNode.createChild("MediaNode")
            node.contentType = contentType
            template = nft.nft_template
            node.sourceNft = nft
            node.key = template.id
            node.contractAddress = template.address
            node.imageUrl = nft.meta.image
            node.title = nft._name
            node.subtitle = template.edition_name
            node.description = template.description
            node.tokenId = nft.token_id_str
            'node.tenant = fetched later from /nft/info/{ictr}/{tokenId}
        end for
        return rootNode
    end function

    ' Creates a child in [rootNode] for every item in [items].
    ' Usually the result will be used in [MediaItemCard] (potentiall via the [MyMediaItemWrapper] delegate)
    sub mediaItemsToNodes(items, rootNode, itemHeight, contentType = "media")
        for each item in items
            node = rootNode.createChild("AAContentNode")
            node.aa = item

            node.addField("contentType", "string", false)
            node.contentType = contentType

            aspectRatio = lcase(item.image_aspect_ratio ?? "") = "wide" ? 16 / 9 : 1
            node.addField("FHDItemWidth", "float", false)
            node.FHDItemWidth = itemHeight * aspectRatio
        end for
    end sub

    sub launchMedia(mediaItem)
        if mediaItem.media_type = NftOps.MediaType.VIDEO or mediaItem.media_type = NftOps.MediaType.LIVE
            videoPlayerView = CreateObject("roSGNode", "VideoPlayer")
            videoPlayerView.videoItem = mediaItem
            router()@.show({ view: videoPlayerView })
        else if mediaItem.media_type = NftOps.MediaType.IMAGE or mediaItem.media_type = NftOps.MediaType.GALLERY
            galleryView = CreateObject("roSGNode", "ImageGallery")
            galleryView.galleryItem = mediaItem
            router()@.show({ view: galleryView })
        else if mediaItem.media_file <> invalid
            qrView = createObject("roSGNode", "ExternalContentQrDialog")
            qrView.link = NftOps.assetLinkToUrl(mediaItem.media_file)
            createObject("roSGNode", "FullscreenDialog").showView = qrView
        else 
            Toast.show("Unknown media type")
        end if
    end sub
end namespace
