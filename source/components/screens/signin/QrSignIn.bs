import "pkg:/components/utils/promise.bs"
import "pkg:/components/utils/RouterUtils.bs"
import "pkg:/components/http/http.bs"
import "pkg:/components/utils/UrlUtil.bs"
import "pkg:/components/utils/Toast.bs"
import "pkg:/components/screens/signin/providers/LoginProvider.bs"
import "pkg:/components/screens/signin/UrlShortener.bs"
import "pkg:/components/screens/signin/SignInUtil.bs"
import "pkg:/components/stores/stores.bs"

sub init()
    m.expirationTimer = m.top.findNode("code_expiration_timer")
    m.expirationTimer.ObserveField("fire", "getNewCode")

    m.statusTimer = m.top.findNode("status_poll_timer")
    m.statusTimer.ObserveField("fire", "checkStatus")
end sub

sub onPropertyChanged()
    property = m.top.property
    login = property?.login

    saveViewReferences()

    if m.bgImage <> invalid
        uri = login?.styling?.background_image_tv ?? login?.styling?.background_image_desktop
        m.bgImage.uri = UrlUtil.imageLinkToUrl(uri, m.bgImage.height)
    end if

    ' Technically "ory" isn't the only possible provider, but it's the only one we are currently using,
    ' so we just assume it's "ory" all the time.
    m.loginProvider = new OryProvider(property)
    getNewCode()
end sub

sub saveViewReferences()
    m.bgImage = m.top.findNode("bgImage")

    m.qrCode = m.top.findNode("qr_code")
    m.qrImage = m.top.findNode("qr_image")

    m.newCodeButton = m.top.findNode("btn_new_code")
    m.newCodeButton.ObserveField("buttonSelected", "getNewCode")
    setInitialFocus(m.newCodeButton)

    m.backButton = m.top.findNode("btn_back")
    _ = m.backButton?.observeFieldScoped?("buttonSelected", "onBackClicked")

    m.spinner = m.top.findNode("spinner")
    m.spinner.poster.observeField("loadStatus", "centerSpinner")
    centerSpinner()
end sub

sub getNewCode()
    m.expirationTimer.control = "stop"
    m.statusTimer.control = "stop"

    promises.chain(m.loginProvider.getActivationData()).then(
    function(response)
        activationData = response.json
        provider = m.loginProvider
        m.activationData = activationData
        m.qrCode.text = provider.getCode(activationData)

        m.statusTimer.duration = provider.getPollingInterval(activationData)
        m.statusTimer.control = "start"

        m.expirationTimer.duration = provider.getExpiresIn(activationData)
        m.expirationTimer.control = "start"

        qrUrl = provider.getQrUrl(activationData)
        return shortenUrl(qrUrl)
    end function).then(
    sub(qrUrl)
        size = m.qrImage.boundingRect().width
        m.qrImage.uri = UrlUtil.qrUrlForData(qrUrl, size)
    end sub
    )
end sub

sub onBackClicked()
    if router().viewManager.viewCount = 1
        router().removeStack = "signin"
    else
        m.top.close = true
    end if
end sub

sub checkStatus()
    promises.chain(m.loginProvider.checkToken(m.activationData)).then(
    function(result)
        if result <> "pending"
            ' Assume success. this is VERY optimistic.
            ' We don't need to actually navigate to Dashboard, removing the app stack will
            ' cause [MainRouter] to re-run started logic, realize we're logged in and navigate to Dashboard.
            ' This is required to make sure none of the login flow screens are in the backstack after login.
            m.expirationTimer.control = "stop"
            m.statusTimer.control = "stop"

            ' Re-fetch Properties before navigating away from sign-in
            return stores.propertyStore()@.fetchDiscoverableProperties()
        else
            return result
        end if
    end function).then(
    sub(result)
        if result <> "pending"
            SignInUtil.onAuthSuccess(m.top.property, m.top.nextScreenName, m.top.nextScreenArgs)
        end if
    end sub
    )
end sub

sub centerSpinner()
    if(m.spinner.poster.loadStatus = "ready")
        centerx = (m.qrImage.width - m.spinner.poster.bitmapWidth) / 2
        centery = (m.qrImage.height - m.spinner.poster.bitmapHeight) / 2
        m.spinner.translation = [centerx, centery]
    end if
end sub

function OnKeyEvent(key as string, press as boolean) as boolean
    if press
        ' Roku's ComponentController should take care of this, but once again they have disappointed me.
        if key = "back" and router().viewManager.viewCount = 1
            ' We are the last view in the "signin" stack, remove the stack.
            router().removeStack = "signin"
            return true
        end if

        backButtonFocused = m.backButton?.hasFocus?() ?? false
        if key = "right" and m.newCodeButton.hasFocus()
            return m.backButton.setFocus(true)
        else if key = "left" and backButtonFocused
            return m.newCodeButton.setFocus(true)
        end if
        return false
    end if
end function
