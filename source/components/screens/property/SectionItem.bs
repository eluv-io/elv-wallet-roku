import "pkg:/components/screens/property/rows/row_util.bs"

sub init()
    m.delegatedFields = {}
end sub

sub onContentChanged()
    'TODO reuse?
    m.top.removeChild(m.itemNode)

    sectionItem = m.top.itemContent.aa
    ' This is the magic sacue - we create the actual Node to be used, 
    ' as defined by 'type' (e.g. "CarouselItem", "TextItem", "BannerItem", etc)
    itemNode = m.top.CreateChild(sectionItem.type)

    ' Dump all delegated fields before setting content.
    ' It's easier to call node.update(m.delegatedFields) but it litters the log
    fields = m.delegatedFields
    for each key in fields
        if itemNode.doesExist(key)
            itemNode[key] = fields[key]
        end if
    end for
    if itemNode.doesExist("content")
        itemNode.content = sectionItem
    end if
    m.itemNode = itemNode
end sub

' Saves the values so they can be set on itemNode when it will be created.
' Also fowrads updates to m.itemNode, if it already exists.
sub delegate(event)
    field = event.getField()
    data = event.getData()

    m.delegatedFields.addReplace(field, data)

    if m.itemNode <> invalid and m.itemNode.doesExist(field)
        m.itemNode[field] = data
    end if
end sub