import "pkg:/components/utils/AspectRatio.bs"

namespace DisplaySettings
    ' Media items are themselves a "DisplaySettings"
    function getThumbnailAndRatio(display)
        if display?.thumbnail_image_square <> invalid
            return {
                thumbnail: display.thumbnail_image_square,
                aspectRatio: AspectRatio.SQUARE
            }
        else if display?.thumbnail_image_portrait <> invalid
            return {
                thumbnail: display.thumbnail_image_portrait,
                aspectRatio: AspectRatio.POSTER
            }
        else if display?.thumbnail_image_landscape <> invalid
            return {
                thumbnail: display.thumbnail_image_landscape,
                aspectRatio: AspectRatio.WIDE
            }
        else return invalid
        end if
    end function

    ' Merges [overrides] into [base] and returns the result.
    ' String values in [overrides] will only be applied if they are non-empty
    function applyOverrides(base, overrides)
        result = {}
        result.append(base)
        for each entry in (overrides ?? {}).items()
            value = entry.value
            ' Skip null values
            if value = invalid then continue for
            ' Skip empty strings
            if getInterface(value, "ifString") <> invalid and value = "" then continue for
            result[entry.key] = value
        end for
        return result
    end function
end namespace