import "pkg:/components/utils/AspectRatio.bs"

namespace DisplaySettings
    ' Media items are themselves a "DisplaySettings"
    function getThumbnailAndRatio(display)
        forcedRatio = AspectRatio.fromString(display.aspect_ratio)
        thumbnail = invalid
        ' If aspect ratio is forced, try to use the corresponding thumbnail.
        if forcedRatio = AspectRatio.SQUARE
            thumbnail = display.thumbnail_image_square
        else if forcedRatio = AspectRatio.POSTER
            thumbnail = display.thumbnail_image_portrait
        else if forcedRatio = AspectRatio.WIDE
            thumbnail = display.thumbnail_image_landscape
        end if

        if thumbnail <> invalid
            ' There's a thumbnail defined for the forced aspect ratio
            return {
                thumbnail: thumbnail,
                aspectRatio: forcedRatio
            }
        end if

        ' Aspect ratio is either not set, or the corresponding thumbnail is not defined.
        ' Try to find the best thumbnail, but still use the forced ratio if it exists
        if display?.thumbnail_image_square <> invalid
            return {
                thumbnail: display.thumbnail_image_square,
                aspectRatio: forcedRatio ?? AspectRatio.SQUARE
            }
        else if display?.thumbnail_image_portrait <> invalid
            return {
                thumbnail: display.thumbnail_image_portrait,
                aspectRatio: forcedRatio ?? AspectRatio.POSTER
            }
        else if display?.thumbnail_image_landscape <> invalid
            return {
                thumbnail: display.thumbnail_image_landscape,
                aspectRatio: forcedRatio ?? AspectRatio.WIDE
            }
        else return invalid
        end if
    end function

    ' Merges [overrides] into [base] and returns the result.
    ' String values in [overrides] will only be applied if they are non-empty
    function applyOverrides(base, overrides)
        result = {}
        result.append(base)
        for each entry in (overrides ?? {}).items()
            value = entry.value
            ' Skip null values
            if value = invalid then continue for
            ' Skip empty strings
            if getInterface(value, "ifString") <> invalid and value = "" then continue for
            result[entry.key] = value
        end for
        return result
    end function
end namespace