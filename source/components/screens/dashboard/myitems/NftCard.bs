import "pkg:/components/utils/UrlUtil.bs"
sub init()
    m.bg = m.top.findNode("bg")
    m.container = m.top.findNode("content_container")
    m.tokenId = m.top.findNode("token_id")
    m.image = m.top.findNode("image")
    m.title = m.top.findNode("title")
    m.subtitle = m.top.findNode("subtitle")

    m.image_shimmer = m.top.findNode("image_shimmer")

    m.image.observeFieldScoped("loadStatus", "onImageLoadStatusChanged")
end sub

sub onItemContentChanged()
    content = m.top.itemContent
    if content.title = "placeholder"
        setupPlaceholderView()
        return
    end if

    m.tokenId.text = `#${content.tokenId}`
    m.image.uri = UrlUtil.appendQuery(content.imageUrl, "width", m.image.width)
    m.title.text = content.title
    m.subtitle.text = content.subtitle
end sub

sub onWidthChanged()
    width = m.top.width
    padding = 36
    widthWithPadding = width - (padding * 2)
    m.container.translation = [padding, 0]
    m.bg.width = width
    m.tokenId.width = widthWithPadding

    m.image.width = widthWithPadding
    m.image.height = widthWithPadding

    m.title.width = widthWithPadding
    m.subtitle.width = widthWithPadding
end sub

sub onHeightChanged()
    m.bg.height = m.top.height
end sub

sub onFocusChanged()
    if m.top.gridHasFocus and m.top.focusPercent > 0.8
        m.bg.uri = "pkg:/images/nft_card_focused_bg.png"
    else
        m.bg.uri = "pkg:/images/nft_card_bg.png"
    end if
end sub

sub setupPlaceholderView()
    m.title.text = "wait for it..."
end sub

sub onImageLoadStatusChanged()
    if m.image.loadStatus = "ready"
        m.image_shimmer.control = "stop"
    end if
end sub
