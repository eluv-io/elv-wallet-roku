import "pkg:/components/utils/NftOps.bs"
import "pkg:/components/utils/UrlUtil.bs"
import "pkg:/components/stores/stores.bs"

sub init()
    stores.propertyStore()@.fetchDiscoverableProperties()

    m.root = m.top.findNode("root")
    m.logo = m.top.findNode("logo")
    m.bgImage = m.top.findNode("bgImage")
    grid = m.top.findNode("grid")
    m.grid = grid

    grid.ObserveFieldScoped("currFocusRow", "onFocusRowChanged")
    grid.ObserveFieldScoped("itemSelected", "onPropertySelected")
    grid.ObserveFieldScoped("itemFocused", "onPropertyFocused")
    ' When the logo gains focus it means the grid is no longer focused
    m.logo.ObserveFieldScoped("focusedChild", "onLogoFocused")

    stores.propertyStore().ObserveFieldScoped("properties", "onPropertiesChanged")

    'Queue a timer for 10ms just to skip the first render pass and wait for parent to be attached
    time.setTimer(initAfterParenting, 0.01)
end sub

sub initAfterParenting()
    m.top.getParent().observeFieldScoped("translation", "onParentTranslationChanged")
    onParentTranslationChanged()

    ' init with dummy cards that shimmer while we load real data
    if m.grid.content = invalid
        content = CreateObject("roSGNode", "ContentNode")
        for i = 1 to 10
            content.createChild("ContentNode")
        end for
        m.grid.content = content
    end if
end sub

sub onParentTranslationChanged()
    offset = m.top.getParent?()?.translation?[0] ?? 0
    padding = m.root.translation[0]

    ' Calc grid itemSize so it fits [numColumns] with equal padding on left/right
    cols = m.grid.numColumns
    itemWidth = (1920 - offset - (padding * 2)) / cols
    itemWidth -= m.grid.itemSpacing[0]
    itemHeight = itemWidth * 3 / 2
    m.grid.itemSize = [itemWidth, itemHeight]

    ' Give bgImage negative translation to make sure it starts at the global x=0,
    ' even if parent is translated to have an offset.
    m.bgImage.translation = [-offset, 0]
end sub

sub onPropertySelected()
    property = m.grid.content.getchild(m.grid.itemfocused)?.aa
    if property = invalid
        Toast.show("Error loading property")
        return
    end if
    loginProvider = property.login?.settings?.provider ?? "auth0"
    if stores.tokenStore().loginProvider = loginProvider
        Toast.show("TODO: Impl property details page")
    else
        signIn = CreateObject("roSGNode", "SignIn")
        signIn.property = property
        router()@.show({ view: signIn })
    end if
end sub

' The logo is our "focus placeholder" for when we want to have nothing selected in the grid
sub onLogoFocused()
    if m.logo.IsInFocusChain()
        m.bgImage@.changeImage("")
    end if
end sub

sub onPropertyFocused()
    property = m.grid.content?.getchild?(m.grid.itemfocused)?.aa
    if property = invalid
        return
    end if
    m.bgImage@.changeImage(UrlUtil.imageLinkToUrl(property.image_tv, m.bgImage.height))
end sub

sub onFocusRowChanged()
    ' Offset the entire Discover screen to move the logo up when scrolling the Property list
    rowHeight = m.grid.itemSize[1]
    offset = rowHeight * m.grid.currFocusRow
    maxOffset = m.logo.height + m.logo.translation[1]
    if offset > (maxOffset)
        offset = maxOffset
    end if
    m.root.translation = [m.root.translation[0], -offset]
end sub

sub onPropertiesChanged(event)
    properties = event.getData()
    content = CreateObject("roSGNode", "ContentNode")
    if properties <> invalid and properties.count() > 0
        for each property in properties
            content.createChild("AAContentNode").aa = property
        end for
    end if
    m.grid.content = content
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if press
        if key = "up" and m.grid.IsInFocusChain()
            ' only gets called when grid didn't already handle the "up" event
            m.logo.setFocus(true)
            return true
        else if key = "down" and m.logo.IsInFocusChain()
            m.grid.setFocus(true)
            return true
        end if
    end if
    return false
end function
