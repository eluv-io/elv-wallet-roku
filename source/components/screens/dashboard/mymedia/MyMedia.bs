import "pkg:/components/utils/NftOps.bs"
import "pkg:/components/utils/RouterUtils.bs"
import "pkg:/components/stores/stores.bs"

sub init()
    m.itemsList = m.top.findNode("my_items_list")
    m.itemsList.observeFieldScoped("rowItemSelected", "onRowItemSelected")

    stores.contentStore().observeField("nfts", "onNftsChanged")
    onNftsChanged()
end sub

sub onNftsChanged()
    nfts = stores.contentStore().nfts
    if nfts = invalid
        return
    end if
    rootNode = createObject("roSGNode", "ContentNode")

    addFeaturedMediaRow(nfts, rootNode)
    addMediaSectionRows(nfts, rootNode)
    addMyItemsRow(nfts, rootNode)

    m.itemsList.content = rootNode
    m.itemsList.numRows = rootNode.getChildCount()
end sub

sub onRowItemSelected()
    rowIndex = m.itemsList.rowItemSelected[0]
    itemIndex = m.itemsList.rowItemSelected[1]
    item = m.itemsList.content.getChild(rowIndex).getChild(itemIndex)
    
    if item.contentType = "nft"
        nftDetail = CreateObject("roSGNode", "NftDetail")
        nftDetail.nft = item
        router()@.show({ view: nftDetail })
    else if item.contentType = "media"
        ' TODO: open media
    end if
end sub

sub addFeaturedMediaRow(nfts, rootNode)
    featuredMedia = []
    for each nft in nfts
        featuredMedia.append(nft._featured_media)
    end for

    if featuredMedia.count() > 1
        featuredMediaRow = rootNode.createChild("ContentNode")

        NftOps.mediaItemsToNodes(featuredMedia, featuredMediaRow, 270)
    end if
end sub

sub addMediaSectionRows(nfts, rootNode)
    for each nft in nfts
        mediaItems = []
        for each section in nft._sections
            for each collection in section.collections
                mediaItems.append(collection.media)
            end for
        end for
        if mediaItems.count() > 0
            nftRow = rootNode.createChild("ContentNode")
            nftRow.title = nft._name
            NftOps.mediaItemsToNodes(mediaItems, nftRow, 270)
        end if
    end for
end sub

sub addMyItemsRow(nfts, rootNode)
    row = rootNode.createChild("AAContentNode")
    row.title = "Items"
    row.aa = { fontStyle: "carousel_36" }
    NftOps.nftsToContentNode(nfts, row, "nft")
end sub
