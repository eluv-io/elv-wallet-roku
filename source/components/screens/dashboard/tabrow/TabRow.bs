import "pkg:/components/screens/dashboard/tabrow/Tabs.bs"
sub init()
    m.top.setFocus(true)

    grid = m.top.findNode("grid")
    m.grid = grid

    content = createObject("roSGNode", "ContentNode")
    content.createChild("ContentNode").title = Tabs.MyItems
    content.createChild("ContentNode").title = Tabs.MyMedia
    content.createChild("ContentNode").title = Tabs.Profile
    grid.content = content

    grid.observeField("itemFocused", "onGridFocusChanged")
end sub

sub onGridFocusChanged()
    grid = m.grid
    tabName = grid.content.getchild(grid.itemFocused).title
    print "[TabRow] Selected tab: "; tabName
    m.top.focusedTabName = tabName
end sub


sub onFocusedTabNameChanged()
    ' Tab might have been set externally. Find the relevant item in the grid and make the grid focus on it
    for i = 0 to m.grid.content.count() - 1
        if m.grid.content.getChild(i).title = m.top.focusedTabName
            m.grid.animateToItem = i
            return
        end if
    end for
end sub
