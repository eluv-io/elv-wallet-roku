import "pkg:/components/screens/dashboard/Tabs.bs"
import "pkg:/components/utils/Toast.bs"
import "pkg:/components/stores/stores.bs"

sub init()
    m.root = m.top.findNode("root")
    m.tabContainer = m.top.findNode("tab_container")
    sideNav = m.top.findNode("sideNav")
    m.sideNav = sideNav

    sideNav.ObserveField("selectedTab", "onTabSelected")
    onTabSelected()
end sub

sub onTabSelected()
    ' Maybe cache nodes for re-use? I'm not sure what the memory implcations will be..
    currentTab = m.sideNav.selectedTab
    if m.lastSelectedTab <> currentTab
        m.lastSelectedTab = currentTab
        m.tabContainer.removeChildIndex(0)
        m.tabContainer.createChild(currentTab)
    end if
    m.tabContainer.getChild(0).setFocus(true)
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    handled = false
    if press
        if key = "right" and m.sideNav.IsInFocusChain()
            m.tabContainer.getChild(0).setFocus(true)
            handled = true
        else if (key = "left" or key = "back") and m.tabContainer.IsInFocusChain()
            m.sideNav.setFocus(true)
            handled = true
        end if
    end if
    return handled
end function