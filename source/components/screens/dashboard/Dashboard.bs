import "pkg:/components/stores/stores.bs"

sub init()
    m.tabContainer = m.top.findNode("tab_container")
    tabRow = m.top.findNode("tab_row")
    m.tabrow = tabrow
    
    tabRow.ObserveField("focusedTabName", "onTabFocused")
    onTabFocused()
end sub

sub onTabFocused()
    ' Re-fetch data on tab changes
    stores.contentStore()@.fetchWalletData()
    
    ' Maybe cache nodes for re-use? I'm not sure what the memory implcations will be..
    currentTab = m.tabrow.focusedTabName
    m.tabContainer.removeChildIndex(0)
    if currentTab = "My Items"
        m.tabContainer.createChild("MyItems")
    else if currentTab = "My Media"
        m.tabContainer.createChild("MyMedia")
    else if currentTab = "Profile"
        m.tabContainer.createChild("Profile")
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    handled = false
    if press
        if key = "down" and m.tabRow.IsInFocusChain()
            m.tabContainer.getChild(0).setFocus(true)
            handled = true
        else if key = "up" and m.tabContainer.IsInFocusChain()
            m.tabRow.setFocus(true)
            handled = true
        end if
    end if
    return handled
end function