import "pkg:/components/utils/UrlUtil.bs"
sub init()
    m.bg = m.top.findNode("bg")
    m.container = m.top.findNode("content_container")
    m.tokenId = m.top.findNode("token_id")
    m.image = m.top.findNode("image")
    m.title = m.top.findNode("title")
    m.subtitle = m.top.findNode("subtitle")

    m.image_shimmer = m.top.findNode("image_shimmer")
    m.title_shimmer = m.top.findNode("title_shimmer")
    m.subtitle_shimmer = m.top.findNode("subtitle_shimmer")

    m.image.observeFieldScoped("loadStatus", "onImageLoadStatusChanged")
    onFocusChanged()
end sub

sub onItemContentChanged()
    content = m.top.itemContent
    if content.title = "placeholder"
        return
    end if

    m.tokenId.text = `#${content.tokenId}`
    m.image.uri = UrlUtil.appendQuery(content.imageUrl, "width", m.image.width)
    m.title.text = content.title
    m.title_shimmer.control = "stop"
    m.subtitle.text = content.subtitle
    m.subtitle_shimmer.control = "stop"
end sub

sub onWidthChanged()
    width = m.top.width
    padding = 24
    widthWithPadding = width - (padding * 2)
    m.container.translation = [padding, 0]
    m.bg.width = width
    m.tokenId.width = widthWithPadding

    m.image.width = widthWithPadding
    m.image.height = widthWithPadding

    m.title.width = widthWithPadding
    m.subtitle.width = widthWithPadding
end sub

sub onHeightChanged()
    m.bg.height = m.top.height
end sub

sub onFocusChanged()
    ' When the card is inside a MarkupGrid (MyItems view), gridHasFocus will be set by the parent.
    ' When it's inside a RowList, rowFocusPercent/rowListHasFocus will be set.
    containerFocused = m.top.gridHasFocus or (m.top.rowListHasFocus and m.top.rowFocusPercent > 0.8)
    if containerFocused and m.top.focusPercent > 0.8
        m.bg.uri = "pkg:/images/nft_card_focused_bg.png"
        m.title.color = "#000000"
        m.subtitle.color = "#646464"
        m.tokenId.color = "#000000"
    else
        m.bg.uri = "pkg:/images/nft_card_bg.png"
        m.title.color = "#FFFFFF"
        m.subtitle.color = "#7A7A7A"
        m.tokenId.color = "#7A7A7A"
    end if
end sub

sub onImageLoadStatusChanged()
    if m.image.loadStatus = "ready"
        m.image_shimmer.control = "stop"
    end if
end sub
