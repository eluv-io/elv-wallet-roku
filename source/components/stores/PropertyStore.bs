import "pkg:/components/http/apis/PropertyApi.bs"
import "pkg:/components/utils/str.bs"
import "pkg:/components/stores/permissions/PermissionsResolver.bs"
import "pkg:/components/utils/Logger.bs"
import "pkg:/components/utils/promise.bs"

sub init()
    ' m.top.properties is an array of properties that show up on the Discover page.
    ' m.propertyMap is a cache of ALL properties keyed by their IDs.
    ' This includes properties individually fetched (e.g. from links or subproperty switching)
    m.propertyMap = {}

    ' Keys are propertyId, and values are a map of PageId->Page object
    m.pageMap = {}
end sub

sub wipe(_ = invalid)
    m.top.properties = []
    m.top.sections = {}
    m.propertyMap = {}
    m.pageMap = {}
end sub

function fetchDiscoverableProperties(_ = invalid)
    Logger.log("Starting Discover Property fetch")
    return promises.onThen(
    PropertyApi.discoverableProperties(),
    sub (result)
        properties = result.json.contents ?? []
        propertyMap = {}
        pageMap = m.pageMap

        for each item in properties
            propertyMap[item.id] = item
            pages = pageMap[item.id] ?? {}
            pages[item.main_page.id] = item.main_page
            pageMap[item.id] = pages
            PermissionResolver.resolvePermissions(item, invalid, item.permission_auth_state)
        end for

        m.propertyMap = propertyMap
        m.pageMap = pageMap
        m.top.properties = properties
        Logger.log("Properties fetched and saved")
    end sub)
end function

' Returns a Promise that will resolve to a Property object.
' Property is only fetched from network if it's not in cache.
function getPropertyById(propertyId)
    cachedProperty = m.propertyMap?.lookup?(propertyId)
    if cachedProperty <> invalid
        Logger.log(`Property found in cache, not fetching from network. (id=${propertyId})`)
        return promises.resolve(cachedProperty)
    end if

    ' Property not in cache - fetch it from network
    return promises.chain(PropertyApi.getPropertyById(propertyId)).then(function(result)
        property = result.json
        Logger.log(`Fetched property from network (id=${property.id})`)
        PermissionResolver.resolvePermissions(property, invalid, property.permission_auth_state)
        propMap = m.propertyMap
        propMap[property.id] = property
        m.propertyMap = propMap
        return property
    end function).toPromise()
end function

function getPageById(property, pageId)
    cachedPage = m.pageMap[property.id]?.lookup?(pageId)
    if cachedPage <> invalid
        Logger.log(`Page found in cache. Not fetching from network (id=${pageId})`)
        return promises.resolve(cachedPage)
    end if

    return promises.chain(PropertyApi.getPageById(property.id, pageId), property).then(function(result, property)
        page = result.json
        PermissionResolver.resolvePermissions(page, property.permissions._content, property.permission_auth_state)
        pages = m.pageMap[property.id] ?? {}
        pages[page.id] = page
        return page
    end function).toPromise()
end function

function fetchSections(property, page)
    Logger.log(source_function_name)
    context = {
        key: `${property.id}_${page.id}`
        pageContentPermissions: page.permissions._content,
        authStates: property.permission_auth_state
    }
    return promises.chain(
    PropertyApi.sectionsById(property.id, page.layout.sections),
    context
    ).then(sub(response, context)
        sections = response.json.contents
        for each section in sections
            try
                PermissionResolver.resolvePermissions(section, context.pageContentPermissions, context.authStates)
            catch e
                Logger.log(`Error parsing permissions for section ${section.id}`)
            end try
        end for
        Logger.log(`Saved sections for ${context.key}`)
        allSections = m.top.sections
        allSections[context.key] = sections
        m.top.sections = allSections
    end sub)
end function