import "pkg:/components/http/apis/PropertyApi.bs"
import "pkg:/components/utils/str.bs"
import "pkg:/components/stores/permissions/PermissionsResolver.bs"
import "pkg:/components/utils/promise.bs"

sub init()
    m.top.properties = []
end sub

function fetchDiscoverableProperties(_ = invalid)
    print "[PropertyStore] Starting Discover Property fetch"
    return promises.onThen(
    PropertyApi.discoverableProperties(),
    sub (result)
        properties = result.json.contents ?? []

        ' Skip permission resolution if not logged in
        if strNotEmpty(stores.tokenStore().fabricToken)
            for each item in properties
                PermissionResolver.resolvePermissions(item, invalid, item.permission_auth_state)
            end for
        end if

        m.top.properties = properties
        print "Properties fetched and saved"
    end sub)
end function
